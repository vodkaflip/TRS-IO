Name      TRS-IO;
Partno    ATF16V8B;
Date      02/27/2018;
Rev       02;
Designer  AP;
Company   SFSU;
Assembly  None;
Location  None;
Device    g16v8a;

/* Inputs  */
Pin 1   = FREHD_ENABLED;
Pin [2..9] = [A7..0];
Pin 17  =  !RD_N;
Pin 18  =  !WR_N;
Pin 19  =  !IORQ_N;
Pin 12  =  !ESP_WAIT_N;

/* Outputs */
Pin 14  =  EXTIO_SEL;
Pin 15  =  WAIT;
Pin 13  =  !ESP_SEL_N;
Pin 16  =  ESP_SEL_TRS_IO;


/* Equations */
io_bus_sel = IORQ_N & (RD_N # WR_N);

/* ESP is selected at port 31 for TRS-IO */
esp_sel_trs_io = !A7 & !A6 & !A5 & A4 & A3 & A2 & A1 & A0 & io_bus_sel;
ESP_SEL_TRS_IO = esp_sel_trs_io;

/* ESP is selected at ports 0xC0-0xCF for FreHD */
esp_sel_frehd = FREHD_ENABLED & A7 & A6 & !A5 & !A4 & io_bus_sel;

/* ESP is selected at either TRS-IO or FreHD ports */
esp_sel = esp_sel_trs_io # esp_sel_frehd;
ESP_SEL_N = esp_sel;

/* ESP_WAIT_N is generated by the ESP */
WAIT = esp_sel & ESP_WAIT_N;

/* EXTIO_SEL_N allows the TRS-80 to read */
EXTIO_SEL = esp_sel & RD_N;
