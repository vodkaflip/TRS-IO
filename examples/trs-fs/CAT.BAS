10 REM THIS IS A TEST OF TRS_FS
20 PRINT "ENTER FILE NAME ";:INPUT Q$
30 OUT 236,16           :REM ENABLE MODEL III BUS
40 OUT 31,4
50 OUT 31,1		:REM OPEN COMMAND
60 L = LEN(Q$)
70 FOR T = 1 TO L:T$=MID$(Q$,T,1):OUT 31,ASC(T$):NEXT T
80 OUT 31,0
90 OUT 31,1              :REM READ
100 GOSUB 1000
110 Y=INP(31)           :REM GET THE STATUS
120 IF Y <> 0 THEN PRINT "ERROR OPENING FILE": END
130 FD=INP(31)	        :REM STORE THE FILE DESCRIPTOR
140 REM READ FROM FILE
150 OUT 31,4
160 OUT 31,3
170 OUT 31,FD
180 OUT 31,255:OUT 31,255:OUT 31,0:OUT 31,0 :REM DATA LENGTH REQUESTED
190 GOSUB 1000
200 Y = INP(31)
210 IF Y <> 0 THEN PRINT "ERROR READING": GOTO 500
220 L1 = INP(31):L2 = INP(31):L3 = INP(31):L4 = INP(31)
230 L = (L2 * 256) + L1
240 FOR T = 1 TO L: Y=INP(31): PRINT CHR$(Y);:NEXT T
500 REM CLOSE
505 OUT 31,4
510 OUT 31,4
520 OUT 31,FD
525 GOSUB 1000
530 Y = INP(31)
540 IF Y <> 0 THEN PRINT "ERROR ON CLOSE"
550 END
1000 IF PEEK(293) = 73 THEN GOTO 1030
1010 IF (PEEK(14304) AND 32) <> 0 THEN GOTO 1010
1020 RETURN
1030 IF (INP(224) AND 8) <> 0 THEN GOTO 1030
1040 RETURN
